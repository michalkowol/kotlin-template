plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.jmailen.kotlinter' version '1.15.1'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'

defaultTasks 'run'

mainClassName = 'com.michalkowol.BootKt'
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task fatJar(type: Jar) {
    baseName = "${project.name}-assembly"
    manifest = jar.manifest
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

sourceSets {
    integrationTest {
        kotlin {
            srcDirs += 'src/it/kotlin'
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources.srcDirs += 'src/it/resources'
    }
}

task integrationTest(group: 'verification', type: Test, description: 'Runs integration tests.') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task stage(dependsOn: ['fatJar', 'build', 'clean'])
build.mustRunAfter clean
fatJar.mustRunAfter build
stage.dependsOn(fatJar)

jacoco {
    toolVersion = '0.8.0'
}

jacocoTestReport {
    reports {
        csv.enabled false
        html.enabled true
        xml.enabled true
    }
    dependsOn test
}

task ktLint(type: org.jmailen.gradle.kotlinter.tasks.LintTask, group: 'verification', description: 'Kotlin lint') {
    source = file('src')
    continuationIndentSize = 4
    reports = [:]
}

kotlinter {
    continuationIndentSize = 4
}

test {
    useJUnitPlatform()
}

check {
    dependsOn = check.dependsOn + [ktLint, integrationTest]
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'http://dl.bintray.com/kotlin/kotlinx.coroutines' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    ext.kotlinxcoroutines = '0.24.0'
    ext.rinca = '0.9.0'

    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.51'
    compile 'org.jetbrains.kotlin:kotlin-reflect:1.2.51'

    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.2.3'

    compile 'com.sparkjava:spark-core:2.7.2'
    compile 'com.google.inject:guice:4.2.0'
    compile 'com.typesafe:config:1.3.3'
    compile 'com.github.kittinunf.result:result:1.5.0'

    compile 'com.zaxxer:HikariCP:3.2.0'
    compile 'org.flywaydb:flyway-core:5.1.4'
    compile 'com.h2database:h2:1.4.197'
    compile 'org.postgresql:postgresql:42.2.4'

    compile "com.github.softwareberg.rinca:database:$rinca"
    compile "com.github.softwareberg.rinca:httpclient:$rinca"
    compile "com.github.softwareberg.rinca:json:$rinca"
    compile "com.github.softwareberg.rinca:xml:$rinca"

    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxcoroutines"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinxcoroutines"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-nio:$kotlinxcoroutines"

    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    testCompile 'com.nhaarman:mockito-kotlin:1.6.0'
    testCompile 'org.powermock:powermock-api-mockito2:2.0.0-beta.5'
    testCompile 'org.assertj:assertj-core:3.10.0'

    integrationTestCompile 'com.ninja-squad:DbSetup-kotlin:2.1.0'
    integrationTestCompile 'com.despegar:spark-test:1.1.8'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}
